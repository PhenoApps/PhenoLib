apply plugin: "com.android.library"
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'
apply from: "../dependencies.gradle"

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.publishVersionCode
        versionName versions.publishVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions { exclude "META-INF/DEPENDENCIES" }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "com.google.android.material:material:1.2.1"
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    testImplementation "junit:junit:4.13.1"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"

    implementation "com.android.volley:volley:1.1.1"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "io.swagger:swagger-annotations:1.6.1"
    implementation "org.apache.httpcomponents:httpcore:4.4.13"
    implementation "org.apache.httpcomponents:httpmime:4.5.12"

    implementation "com.google.code.gson:gson:2.8.6"
    implementation "com.squareup.okhttp:okhttp:2.7.5"
    implementation "com.squareup.okhttp:logging-interceptor:2.7.5"
    implementation "io.gsonfire:gson-fire:1.8.4"
    implementation "io.swagger.core.v3:swagger-annotations:2.1.2"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
    implementation "org.threeten:threetenbp:1.4.2"
}

ext {
    mGroupId = "org.phenoapps.phenolib"
    mArtifactId = "phenolib"
    mVersionCode = versions.publishVersionCode

    mLibraryName = "PhenoLib"
    mLibraryDescription = "A library for PhenoApps"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId mGroupId
                artifactId mArtifactId
                version versions.publishVersion

                from components.release

                artifact androidSourcesJar

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/PhenoApps/PhenoLib")
                credentials {
                    username = System.getenv("GPR_USER")
                    password = System.getenv("GPR_KEY")
                }
            }
        }
    }
}

publish.dependsOn assemble